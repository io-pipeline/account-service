pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'account-service'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            from("ai.pipestream:pipeline-bom-catalog:0.2.2")
        }
    }

    repositories {
        // Prefer Maven Central but allow locally published releases for development
        mavenLocal {
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        mavenCentral()

        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // GitHub Packages for ai.pipestream catalog and dependencies
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                url = uri("https://maven.pkg.github.com/ai-pipestream/bom")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                content {
                    includeGroupByRegex "ai\\.pipestream(\\..*)?"
                }
            }
        }

        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
